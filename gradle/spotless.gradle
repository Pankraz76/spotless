apply plugin: 'com.diffplug.spotless'

spotless {
	groovyGradle {
		target '*.gradle', 'gradle/*.gradle'
		greclipse().configFile rootProject.files('gradle/spotless.eclipseformat.xml', 'gradle/spotless.groovyformat.prefs')
	}
	format 'dotfiles', {
		target '.gitignore', '.gitattributes', '.editorconfig'
		leadingTabsToSpaces(2)
		trimTrailingWhitespace()
		endWithNewline()
	}
	def noInternalDepsClosure = {
		String text = it
		/*
		 * No good way to get around using this import:
		 * https://github.com/gradle/gradle/issues/3191
		 */
		String regex = "import org\\.gradle\\.api\\.internal\\.(?!plugins\\.DslObject)(?!project\\.ProjectInternal)"
		if ((text.contains('import org.gradle.internal.') || text.find(regex))
				&& !text.contains('def noInternalDepsClosure')) {
			throw new AssertionError("Accidental internal import")
		}
	}
	java {
		target '*.gitignore'
		ratchetFrom 'origin/main'
		bumpThisNumberIfACustomStepChanges(1)
		licenseHeaderFile rootProject.file('gradle/spotless.license')
		importOrderFile   rootProject.file('gradle/spotless.importorder')
		eclipse().configFile rootProject.file('gradle/spotless.eclipseformat.xml')
		trimTrailingWhitespace()
		removeUnusedImports()
		formatAnnotations()
		custom 'noInternalDeps', noInternalDepsClosure
	}
}
